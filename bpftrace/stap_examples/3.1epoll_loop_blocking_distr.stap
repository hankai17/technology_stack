#!/usr/bin/env stap

global begin, stats

probe syscall.epoll_wait.return {
    if (target() == pid()) {
        begin = gettimeofday_ms()
    }
}

probe syscall.epoll_wait {
    if (target() == pid() && begin > 0) {
        stats <<< gettimeofday_ms() - begin
    }
}

probe timer.s(5) {
    exit()
}

probe end {
    if (begin == 0) {
        printf("No samples observed so far.\n");

    } else {
        printf("Distribution of epoll loop blocking latencies (in milliseconds)\n")
        printf("max/avg/min: %d/%d/%d\n", @max(stats), @avg(stats), @min(stats))
        print(@hist_log(stats))
    }
}

probe begin {
    printf("Start tracing %d (/usr/local/nginx/sbin/nginx)...\nPlease wait for %d seconds.\n\n",
           target(), 5)
}

#./2.2epoll-loop-blocking-distr.stap --skip-badvars -DMAXACTION=100000 -DMAXMAPENTRIES=5000 -DMAXBACKTRACE=200 -x 11945 
